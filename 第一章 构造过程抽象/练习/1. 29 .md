1，```h=(b-a)/n```：
```
(define h (/ (- b a)
             n))
```

2,$y_{k}=f(a+kh)$：
```
(define (y k)
    (f (+ a (* k h))))
```
3,$y_{k}$有三个不同的乘法因子：
* 当 $k=0$ 或 $k=n$ 时，乘法因子为 1
* 当 $k$ 为奇数时，乘法因子为 4
* 当 $k$ 为偶数时，乘法因子为 2

根据这三条规则，可以给出函数 ```factor``` ，它接受一个参数 $k$ ，返回相应的 $y_{k}$ 的乘法因子：
```
(define (factor k)
    (cond ((or (= k 0) (= k n))
            1)
          ((odd? k)
            4)
          (else
            2)))
```

### 完整辛普森函数：
```
(define (simpson f a b n)
    
    (define h (/ (- b a) n))

    (define (y k)
        (f (+ a (* k h))))

    (define (factor k)
        (cond ((or (= k 0) (= k n))
                1)
              ((odd? k)
                4)
              (else
                2)))
    
    (define (term k)
        (* (factor k)
           (y k)))

    (define (next k)
        (+ k 1))

    (if (not (even? n))
        (error "n can't be odd")
        (* (/ h 3)
           (sum term (exact->inexact 0) next n))))
```

```sum```函数:
```
(define (sum term a next b)
    (if (> a b)
        0
        (+ (term a)
           (sum term (next a) next b))))

```



可以看到，和书本 39 页 ```integral``` 函数计算出的积分相比， simpson 的计算结果精度更高。

